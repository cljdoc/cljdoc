(ns update-github-emojis
  "The complete list of github emojis is available at https://api.github.com/emojis.

  Data is not explicitly described.
  If the url contains unicode, the emojis is rendered as unicode.
  If not the img url should be used.

  For url entries the filename includes what looks like unicode.
  And it is often correct, it is not always.
  For multi-code-point chars, so we cannot rely on it.

  So we send an md doc to github to get back what it actually renders.

  I've noticed that not all emojis render nicely even on modern browsers.
  Maybe this is because I am testing on Linux."
  (:require [babashka.http-client :as http]
            [cheshire.core :as json]
            [clojure.pprint :as pprint]
            [clojure.string :as str]))

(defn- fetch-emojis []
  (->> (http/get "https://api.github.com/emojis")
       :body
       json/decode
       (reduce-kv (fn [m k v]
                    (assoc m k {:img-url v
                                :type (if (str/includes? v "/unicode/")
                                        :unicode
                                        :image)}))
                  (sorted-map))))

(defn- render-emojis [emojis]
  (let [md (reduce (fn [acc n]
                     (str acc (format "`%s`:%s:\n\n"
                                      n n)))
                   ""
                   (->> emojis
                        (filter (fn [[_k v]] (= :unicode (:type v))))
                        (map first)
                        sort))]
    (->> (http/post "https://api.github.com/markdown"
                    {:headers {:content-type "application/json"}
                     :body (json/encode {:text md})})
         :body)))

(defn- parse-rendered-emojis
  "Each typically looks like: <p><code>emoji-tag</code>unicode</p>
   can also render as, ex: <p><code>o2</code><g-emoji class=\"g-emoji\" alias=\"o2\">üÖæÔ∏è</g-emoji></p>
   we won't care about the meaning of the g-emoji wrapper and just use the unicode text."
  [html]
  (reduce (fn [acc n]
            (let [[_ emoji-tag rendering] (re-matches #"<p><code>(.+?)</code>(.+?)</p>" n)
                  unicode (if (str/starts-with? rendering "<g-emoji")
                            (last (re-matches #".*>(.+?)</g-emoji>" rendering))
                            rendering)]
              (assoc acc emoji-tag unicode)))
          (sorted-map)
          (str/split-lines html)))

(defn -main [& _args]
  (let [emojis (fetch-emojis)
        rendered (-> emojis render-emojis parse-rendered-emojis)
        joined (reduce (fn [acc [k v]]
                         (assoc-in acc [k :unicode] v))
                       emojis
                       rendered)]
    (spit "resources/emojis.edn" (str ";; DO NOT EDIT: Generated by bb update-github-emojis\n\n"
                                      (with-out-str (pprint/pprint joined))))))

